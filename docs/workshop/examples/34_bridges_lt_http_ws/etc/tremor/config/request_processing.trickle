define script process
script
  # TODO if we provide parsed url parts as meta variables, this logic will be
  # simpler here (just need check $request.path)
  #
  # also, if we provide configuration of supported rest onramp endpoints as
  # part of the onramp config itself (optionally), we won't need this processing here
  match {"test": $request_url} of
    case r = %{test ~= dissect|%{scheme}://%{host}/%{path_and_rest}|} =>
      match r.test.path_and_rest of
        # only pass requests to /bridge
        case "bridge" =>
          null
        default =>
          emit {
            "error": "Unsupported url path: {r.test.path_and_rest}",
            "event": event
          } => "error"
      end
    default =>
      emit {
        "error": "Failed to parse url: {$request_url}",
        "event": event
      } => "error"
  end;
  event;
end;

create script process;

# main request processing
select event from in into process;
select event from process into out;

# tremor runtime errors from the processing script
# TODO naming here should be standardized to one thing (either err or error)
select event from process/error into err;
